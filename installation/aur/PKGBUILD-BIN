# Maintainer: MuntasirSZN <muntasir.joypurhat@gmail.com>
# Maintainer: cordx56 <cordx56@cordx.cx>

pkgname=rustowl-bin
pkgver=0.3.4
pkgrel=1
pkgdesc='Visualize Ownership and Lifetimes in Rust'
url='https://github.com/wvhulle/rustowl'
license=('MPL-2.0')
makedepends=()
depends=()
conflicts=('rustowl-git' 'rustowl')
arch=('x86_64' 'aarch64')
source_aarch64=("https://github.com/wvhulle/rustowl/releases/download/v${pkgver}/rustowl-aarch64-unknown-linux-gnu.tar.gz")
source_x86_64=("https://github.com/wvhulle/rustowl/releases/download/v${pkgver}/rustowl-x86_64-unknown-linux-gnu.tar.gz")
sha256sums_x86_64=('a75ab50c1678c5cd33f8d5a693c602d60824a865de5aebc3e4775a7987a40838')
sha256sums_aarch64=('3cd4c8ed6b63c91c1bf56fe7127df288a3491853751845f95dd2c0a486f2589f')

package() {
    install -d -m 755 "$pkgdir/opt/rustowl"
    cp -a sysroot/ "$pkgdir/opt/rustowl/"
    install -Dm0755 -t "$pkgdir/usr/bin/" "rustowl"
    install -Dm0755 -t "$pkgdir/usr/bin/" "rustowlc"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/rustowl/LICENSE"
    install -Dm644 man/rustowl.1 "$pkgdir/usr/share/man/man1/rustowl.1"
    install -Dm644 "completions/rustowl.bash" "${pkgdir}/usr/share/bash-completion/completions/rustowl"
    install -Dm644 "completions/_rustowl" "${pkgdir}/usr/share/zsh/site-functions/_rustowl"
    install -Dm644 "completions/rustowl.fish" "${pkgdir}/usr/share/fish/vendor_completions.d/rustowl.fish"
    install -Dm644 "completions/rustowl.elv" "${pkgdir}/usr/share/elvish/completions/rustowl.elv"
    install -Dm644 "completions/_rustowl.ps1" "${pkgdir}/usr/share/powershell/Modules/Rustowl/_rustowl.ps1"
}
